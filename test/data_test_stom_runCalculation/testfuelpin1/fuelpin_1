% Sodium cooled pin cell sample

set acelib "/home/asikth/Documents/xsdata/jeff31/sss_jeff31.xsdata"

set title "FPburn    Sodium cooled pin cell with (U,Pu)O2 fuel"
% set title "Sodium cooled pin cell with (U,Pu)O2 fuel"

set bc 3

set pop 1000 50 20

set gcu      0 1 2 3

plot 3 500 500
% plot 1 500 500



pin 1
fuel 0.440   % fuel pellet outer radius
void 0.46   % cladding inner radius
clad 0.51   % cladding outer radius
sodium       % coolant outside of clad

pin 2        % gas plenum
void 0.46
clad 0.51
sodium 

pin 3        % sodium below and above core
sodium

% Geometry

surf 1 hexxc 0.0 0.0 0.6  % hexagonal lattice surface ( P/D = 1.20 )

surf 2 pz -200
surf 3 pz -100
surf 4 pz  -50
surf 5 pz   50
surf 6 pz   70
surf 7 pz  200

cell 1 0 fill  1    -1  4 -5    % hexagonal lattice filled with fuel pin (universe) 1
cell 2 0 fill  2    -1  3 -4    % hexagonal lattice filled with lower gas plenum, universe 2
cell 3 0 fill  2    -1  5 -6    % hexagonal lattice filled with upper gas plenum, universe 2
cell 4 0 fill  3    -1  2 -3    % hexagonal lattice filled with sodium, universe 3
cell 5 0 fill  3    -1  6 -7    % hexagonal lattice filled with sodium, universe 3

cell 7 0 outside     1  2 -7
cell 8 0 outside       -2
cell 9 0 outside           7

% Fuel

mat fuel  -10  % Oxide fuel with 10 percent porosity 
94238.15c  0.035  % 
94239.15c  0.519  % 
94240.15c  0.238  % Pu vector from spent LWR fuel
94241.15c  0.129  %
94242.15c  0.079  %
92238.15c  4.000  % 80 percent U-238
 8016.15c  10.00  % Two oxygen atoms per metal atom

% Clad

mat clad  -7.84     % Steel cladding density
26054.09c  0.05845  % Composition approximated with pure iron
26056.09c  0.91754  
26057.09c  0.02119  
26058.09c  0.00282

% Coolant

mat sodium -0.86
11023.09c   1.0

% Cross section materials

mat Pu238   -1
94238.09c  1.0

mat Pu239   -1
94239.09c  1.0

mat Pu240   -1
94240.09c  1.0

mat Pu241   -1
94241.09c  1.0

mat Pu242   -1
94242.09c  1.0

set power 400  % Power normalised to 400 W/cm 

set egrid 1e-5 % Cross section grid thinning (to reduce memory requirement)

det 1          % Detector number 1 
du  1          % Flux in pin (universe) number 1

det 2          % Detector number 2 
du  1          % Flux in pin (universe) number 1
dr  18 Pu238   % folded with fission reaction rates
dr  18 Pu239 
dr  18 Pu240 
dr  18 Pu241 
dr  18 Pu242 

det 3          % Detector number 3 
du  1          % Flux in pin (universe) number 1
dr 102 Pu238   % folded with capture reaction rates
dr 102 Pu239
dr 102 Pu240
dr 102 Pu241
dr 102 Pu242


